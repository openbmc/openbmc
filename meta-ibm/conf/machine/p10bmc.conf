DISTRO_NAME = "IBM eBMC (OpenBMC for IBM Enterprise Systems)"

# The first dtb listed is the one used by default on a BMC genesis boot.
# Need bonnell to be the first one because it contains an eeprom that's bigger
# than in the other systems. Otherwise the device driver would truncate the data
# on that eeprom on a bonnell system.
KERNEL_DEVICETREE = \
    "aspeed/aspeed-bmc-ibm-bonnell.dtb \
     aspeed/aspeed-bmc-ibm-rainier.dtb \
     aspeed/aspeed-bmc-ibm-rainier-4u.dtb \
     aspeed/aspeed-bmc-ibm-rainier-1s4u.dtb \
     aspeed/aspeed-bmc-ibm-everest.dtb"
UBOOT_MACHINE = "ast2600_openbmc_spl_emmc_defconfig"
UBOOT_DEVICETREE = "ast2600-p10bmc"
SPL_BINARY = "spl/u-boot-spl.bin"

OBMC_POWER_SUPPLY_INSTANCES = "0 1 2 3"

require conf/distro/include/phosphor-mmc.inc
require conf/machine/include/ast2600.inc
require conf/machine/include/obmc-bsp-common.inc
require conf/machine/include/openpower.inc
require conf/machine/include/p10.inc
require conf/distro/include/openpower-virtual-pnor.inc
require conf/distro/include/openpower-hostfw-over-pldm.inc
require conf/distro/include/ibm-mpreboot.inc
require conf/distro/include/ibm-yaml.inc
require conf/distro/include/google-authenticator-libpam.inc
include conf/distro/include/extra-dev-debug-tools.inc
DISTRO_FEATURES += "ibm-service-account-policy"
MACHINE_FEATURES += "phal tpm2"

SERIAL_CONSOLES = "115200;ttyS4"

MRW_XML = "witherspoon.xml"
PHOSPHOR_MRW_LICENSE = "Apache-2.0"
PHOSPHOR_MRW_LIC_FILES_CHKSUM = "file://LICENSE;md5=e3fc50a88d0a364313df4b21ef20c29e"
PHOSPHOR_MRW_URI = "git://github.com/open-power/witherspoon-xml;branch=master;protocol=https"
PHOSPHOR_MRW_REV = "c622cb5a5dd3ebc2a4eef558d1b70740f914e6f7"

VIRTUAL-RUNTIME_phosphor-hwmon-config:df-mrw = ""

PREFERRED_PROVIDER_virtual/phosphor-fan-control-fan-config:df-mrw = \
    "phosphor-fan-control-fan-config"
PREFERRED_PROVIDER_virtual/phosphor-fan-presence-config:df-mrw = \
    "phosphor-fan-presence-config"

SKIP_BROKEN_MRW = "1"

IMAGE_FEATURES:remove = "obmc-ikvm"

UBOOT_SIGN_ENABLE = "1"
SPL_SIGN_ENABLE = "1"

FIT_HASH_ALG = "sha512"
FIT_SIGN_ALG = "rsa4096"
FIT_SIGN_NUMBITS = "4096"
UBOOT_FITIMAGE_ENABLE = "1"
UBOOT_FIT_HASH_ALG = "sha512"
UBOOT_FIT_SIGN_ALG = "rsa4096"
UBOOT_FIT_SIGN_NUMBITS = "4096"

UBOOT_SIGN_KEYNAME = "rsa_oem_fitimage_key"
SPL_SIGN_KEYNAME = "rsa_oem_fitimage_key"

UBOOT_SIGN_KEYDIR = "${WORKDIR}"
SPL_SIGN_KEYDIR = "${WORKDIR}"

SOCSEC_SIGN_ENABLE = "1"

DEBUG_TRIGGERS = "kcs2"
PACKAGECONFIG:append:pn-debug-trigger = " triggers"

PACKAGECONFIG:pn-opkg = "curl openssl ssl-curl"
PACKAGECONFIG:remove:pn-entity-manager = "ipmi-fru"

# Add zImage to images directory for Qemu
KERNEL_IMAGETYPES +=" zImage "

# Produce a .qcow2 of the wic for Qemu
IMAGE_FSTYPES +=" wic.qcow2 "

QB_ROOTFS_OPT:p10bmc="-drive file=@ROOTFS@,if=sd,format=qcow2,index=2"
QB_MACHINE:p10bmc="-machine rainier-bmc"
QB_DTB:p10bmc="aspeed/aspeed-bmc-ibm-rainier.dtb"
QB_MEM:p10bmc="-m 1024"
QB_DEFAULT_FSTYPE:p10bmc="wic.qcow2"
QB_FSINFO = "wic:no-kernel-in-fs"
QB_DEFAULT_KERNEL:p10bmc="zImage"
QB_KERNEL_ROOT:p10bmc="PARTLABEL=rofs-a"

FLASH_SIZE = "16777216"
