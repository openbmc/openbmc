require conf/distro/openbmc-phosphor.conf
require conf/distro/include/phosphor-isolation.inc
require conf/distro/include/phosphor-no-webui.inc

GBMC_VERSION ?= "0.5.0.0"
DISTRO = "gbmc"
DISTRO_NAME = "gBMC (OpenBMC + Google customizations)"
DISTRO_VERSION = "gbmc-release-${GBMC_VERSION}"
EXTENDED_VERSION = "${DISTRO_VERSION}"
DISTROOVERRIDES .= ":gbmc"

# Possible values: "dev", "prod", "local", "mfg"
GBMC_CONFIG ?= "local"
OVERRIDES .= ":${GBMC_CONFIG}:"
# mfg config includes local config
OVERRIDES:append:mfg = ":local:"
# local config will also include dev config
OVERRIDES:append:local = ":dev:"

DISTRO_FEATURES:remove = "avahi"
DISTRO_FEATURES:remove = "ldap"

# Required to get entity-manager to build with FruDevice service
DISTRO_FEATURES:append = " ipmi-fru"

# Disable u-boot shell for prod builds
DISTRO_FEATURES:append:prod = " disable-u-boot-shell"

WATCHDOG_PKG ?= "watchdog"

SANITY_TESTED_DISTROS:append = " Debian-rodete \n debian-rodete \n "

VIRTUAL-RUNTIME_bmc-update ??= "inplace-gbmc-update"

# Provides reasonable data integrity and faster than sha*
FIT_HASH_ALG = "crc32"

OBMC_ORG_YAML_SUBDIRS += " com/google"

# Force the patches to not have any fuzz for gBMC
ERROR_QA:append = " patch-fuzz"

IGPS_MACHINE_DEFAULT = "EB"
IGPS_MACHINE_DEFAULT:npcm8xx = "Google"
IGPS_MACHINE ?= "${IGPS_MACHINE_DEFAULT}"

# Remove dependencies on python3-core when using nftables
PACKAGECONFIG:remove:pn-nftables = "python"

# Do not configure TPM layer
SKIP_META_TPM_SANITY_CHECK = "1"

# Set /var/log directory to be volatile
PHOSPHOR_VOLATILE_VARLOG = "1"
